import "./std/io" as io
import "./std/sys" as sys


alias PeekerInfo = struct {
    int pos,

    int fd,
    int line,
    int char_of_line,
    chr last_chr,
    bool eof,
    chr* filename,
}

chr EOF = 0


-- read character from file
-- when char is 0 end of file is reached
chr read(PeekerInfo* p) {
    -- don't return extra character if end of file is reached
    if p.eof {
        return '\00'
    }

    if p.last_chr == '\0A' {
        p.line = p.line + 1
        p.char_of_line = 0
    }

    if io.read(p.fd, &p.last_chr, 1) == 0 {
        -- no bytes are read: end of file is reached
        p.last_chr = '\00'
        p.eof = true
        return p.last_chr
    }
    p.char_of_line = p.char_of_line + 1

    p.pos = p.pos + 1
    return p.last_chr
}

void seek(PeekerInfo* p, int pos) {
    io.lseek(p.fd, pos, io.SEEK_SET)
}

PeekerInfo* new(int fd) {
    PeekerInfo* p = (PeekerInfo*) sys.malloc(sizeof(PeekerInfo))
    p.pos = 0
    p.line = 1
    p.char_of_line = 0
    p.fd = fd
    p.last_chr = '\00'
    p.eof = false
    p.filename = ""
    return p
}
