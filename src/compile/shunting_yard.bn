-- implementation of the Shunting-yard_algorithm
-- https://en.wikipedia.org/wiki/Shunting-yard_algorithm#The_algorithm_in_detail
import "../std/sys" as sys
import "../std/io" as io
import "../parser" as parser
import "../std/string" as string

-- implementation of the shunting yard alogirhtm
alias SYStack = struct {
    parser.Node* n,
    SYStack* prev,
}
int SYStack_size = 16

SYStack* stack_new() {
    SYStack* s = (SYStack*) sys.malloc(SYStack_size)
    s.n = null
    s.prev = null
    return s
}

SYStack* sy_algorithm(parser.Node* assignable) {
    out_stack := stack_new()
    op_stack := stack_new()

    token_stack := stack_new()

    parser.Node* ptr
    for ptr = assignable ; ptr != null ; ptr = ptr.next {
        token_stack = stack_push(token_stack, ptr)
    }

    -- while tokens to be read
    for ptr = token_stack.n; ptr != null; ptr = token_stack.n {
        cs := token_stack
        token_stack = token_stack.prev
        sys.free((chr*) cs)
        -- assignable -> put it to the output-queue
        if string.strcmp(ptr.type, "mono_assignable") == 0 {
            out_stack = stack_push(out_stack, ptr)
        } elif string.strcmp(ptr.type, "operator") == 0 {
            quit := false
            for ; quit == false; {
                if op_stack.n == null {
                    -- reached end of the stack
                    quit = true
                } elif op_precedence(op_stack.n) < op_precedence(ptr) {
                    -- pop operator from the stack
                    top := op_stack
                    op_stack = op_stack.prev
                    out_stack = stack_push(out_stack, top.n)
                    sys.free((chr*) top)
                } else {
                    quit = true
                }
            }
            -- push operator in operator stack
            op_stack = stack_push(op_stack, ptr)
        } else {
            io.printf(":coffee-error: node of type %s could not be parsed in sy_algorithm\0A", ptr.type)
        }
    }

    for ; op_stack.n != null ; {
        -- pop operator on the stack to the output queue
        node := op_stack.n
        s := op_stack
        op_stack = op_stack.prev
        sys.free((chr*) s)

        out_stack = stack_push(out_stack, node)
    }

    return out_stack
}

SYStack* stack_push(SYStack* curr_stack, parser.Node* node) {
    SYStack* s = (SYStack*) sys.malloc(SYStack_size)
    s.n = node
    s.prev = curr_stack
    return s
}

int op_precedence(parser.Node* n) {
    -- https://en.cppreference.com/w/c/language/operator_precedence
    op := n.children
    if string.strcmp(op.value, "+") == 0 | string.strcmp(op.value, "-") == 0 {
        return 4
    } elif string.strcmp(op.value, "*") == 0 | string.strcmp(op.value, "/") == 0 {
        return 3
    } elif string.strcmp(op.value, "==") == 0 {
        return 7
    } elif string.strcmp(op.value, ">") == 0 | string.strcmp(op.value, "<") == 0 {
        return 6
    } elif string.strcmp(op.value, "&") == 0 {
        return 11
    } elif string.strcmp(op.value, "|") == 0 {
        return 12
    }
    io.printf(":coffee-error: priority not defined for operator with value: %s\0A", op.value)
    return 4
}

