import "../std/string" as string
import "../std/io" as io
import "../std/sys" as sys


chr* string_to_llvm(chr* text) {
    str_len := string.strlen(text)

    buf := ""

    -- ignore start and end quotes for string length calculation
    for i := 1; i < str_len -1; i = i + 1 {

        if text[i] == '"' {
            io.asprintf(&buf, "%s\\22", buf)
        } elif text[i] == '\x5C' {
            if text[i + 1] == 'n' | text[i + 1] == 'N' {
                io.asprintf(&buf, "%s\\0A", buf)
                i = i + 1
            } elif text[i + 1] == '\x5C' {
                io.asprintf(&buf, "%s\\5C", buf)
                i = i + 1
            } elif text[i + 1] == 'x' {
                -- hex character
                i = i + 2
                io.asprintf(&buf, "%s\\%c%c", buf, string.toupper(text[i]), string.toupper(text[i+1]))
                i = i + 1
            } else {
                io.asprintf(&buf, "%s%c", buf, text[i])
            }
        } else {
            io.asprintf(&buf, "%s%c", buf, text[i])
        }

    }

    io.asprintf(&buf, `c"%s\\00"`, buf)
    return buf
}

int llvm_str_len(chr* text) {
    int len = 0
    for i := 2 ; text[i] != '\00' ; i = i + 1 {
        if text[i] == '\5C' {
            i = i + 2
        }
        len = len + 1
    }
    return len -1
}


chr* chr_to_llvm(chr* chr_repr) {
    int intval
    chr* buf
    chr chrval

    chr_len := string.strlen(chr_repr)
    if chr_len == 6 {
        io.sscanf(chr_repr, "'\\x%x'", &intval)
        io.asprintf(&buf, "%d", intval)
    } elif chr_len == 5 {
        -- CHR in the form of "\XX" (hex numbers)
        io.sscanf(chr_repr, "'\\%x'", &intval)
        io.asprintf(&buf, "%d", intval)

    } elif chr_len == 4 {
        io.sscanf(chr_repr, "'\\%c'", &chrval)
        if chrval == 'n' {
            buf = "10"
        } else {
            return (chr*) null
        }
    } elif chr_len == 3 {
        -- CHR in the form of "X" (single character)
        io.sscanf(chr_repr, "'%c'", &intval)
        io.asprintf(&buf, "%d", intval)
    } else {
        return (chr*) null
    }
    return buf
}
